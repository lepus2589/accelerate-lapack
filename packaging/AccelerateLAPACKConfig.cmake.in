#[[
MIT License

CMake build script for the Accelerate LAPACK project
Copyright (c) 2025 Tim Kaune

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
]]

@PACKAGE_INIT@

cmake_minimum_required(VERSION @CMAKE_MINIMUM_REQUIRED_VERSION@...@CMAKE_MAXIMUM_SUPPORTED_VERSION@)

# Check if BLAS was already searched in the parent project
if (BLAS_FOUND)
    if (NOT BLAS_Accelerate_LIBRARY)
        message(STATUS "BLAS is provided by a different distribution than Accelerate.")
        return()
    endif ()
else ()
    if (DEFINED BLA_VENDOR AND NOT BLA_VENDOR STREQUAL "Apple")
        message(WARNING "BLAS by vendor \"${BLA_VENDOR}\" is requested, but Accelerate will be provided.")
    endif ()
endif ()

# Check if LAPACK was already searched in the parent project
if (LAPACK_FOUND)
    if (NOT LAPACK_Accelerate_LIBRARY)
        message(STATUS "LAPACK is provided by a different distribution than Accelerate.")
        return()
    endif ()
else ()
    if (DEFINED BLA_VENDOR AND NOT BLA_VENDOR STREQUAL "Apple")
        message(WARNING "LAPACK by vendor \"${BLA_VENDOR}\" is requested, but Accelerate will be provided.")
    endif ()
endif ()

include("@PACKAGE_AccelerateLAPACK_INSTALL_CMAKEDIR@/NEW_BLAS_Targets.cmake")
include("@PACKAGE_AccelerateLAPACK_INSTALL_CMAKEDIR@/NEW_LAPACK_Targets.cmake")

set(ACCELERATE_LAPACK_ILAVER_VERSION "@ACCELERATE_LAPACK_ILAVER_VERSION@")

set_target_properties(
    BLAS::NEW_BLAS
    LAPACK::NEW_LAPACK
    BLAS::NEW_BLAS64
    LAPACK::NEW_LAPACK64
    PROPERTIES
    VERSION "${ACCELERATE_LAPACK_ILAVER_VERSION}"
)

get_target_property(BLAS32_LIBRARIES BLAS::NEW_BLAS INTERFACE_LINK_LIBRARIES)
get_target_property(BLAS32_LINKER_FLAGS BLAS::NEW_BLAS INTERFACE_LINK_OPTIONS)
get_target_property(BLAS64_LIBRARIES BLAS::NEW_BLAS64 INTERFACE_LINK_LIBRARIES)
get_target_property(BLAS64_LINKER_FLAGS BLAS::NEW_BLAS64 INTERFACE_LINK_OPTIONS)

get_target_property(LAPACK32_LIBRARIES LAPACK::NEW_LAPACK INTERFACE_LINK_LIBRARIES)
get_target_property(LAPACK32_LINKER_FLAGS LAPACK::NEW_LAPACK INTERFACE_LINK_OPTIONS)
get_target_property(LAPACK64_LIBRARIES LAPACK::NEW_LAPACK64 INTERFACE_LINK_LIBRARIES)
get_target_property(LAPACK64_LINKER_FLAGS LAPACK::NEW_LAPACK64 INTERFACE_LINK_OPTIONS)

if (BLA_SIZEOF_INTEGER EQUAL 8)
    set(BLAS_LIBRARIES ${BLAS64_LIBRARIES})
    set(BLAS_LINKER_FLAGS ${BLAS64_LINKER_FLAGS})
else ()
    set(BLAS_LIBRARIES ${BLAS32_LIBRARIES})
    set(BLAS_LINKER_FLAGS ${BLAS32_LINKER_FLAGS})
endif ()

if (NOT TARGET BLAS::BLAS)
    add_library(BLAS::BLAS INTERFACE IMPORTED)
endif ()

set_target_properties(BLAS::BLAS PROPERTIES INTERFACE_LINK_LIBRARIES "${BLAS_LIBRARIES}")
set_target_properties(BLAS::BLAS PROPERTIES INTERFACE_LINK_OPTIONS "${BLAS_LINKER_FLAGS}")

if (BLA_SIZEOF_INTEGER EQUAL 8)
    set(LAPACK_LIBRARIES ${LAPACK64_LIBRARIES})
    set(LAPACK_LINKER_FLAGS ${LAPACK64_LINKER_FLAGS})
else ()
    set(LAPACK_LIBRARIES ${LAPACK32_LIBRARIES})
    set(LAPACK_LINKER_FLAGS ${LAPACK32_LINKER_FLAGS})
endif ()

if (NOT TARGET LAPACK::LAPACK)
    add_library(LAPACK::LAPACK INTERFACE IMPORTED)
endif ()

set_target_properties(LAPACK::LAPACK PROPERTIES INTERFACE_LINK_LIBRARIES "${LAPACK_LIBRARIES}")
set_target_properties(LAPACK::LAPACK PROPERTIES INTERFACE_LINK_OPTIONS "${LAPACK_LINKER_FLAGS}")

check_required_components(AccelerateLAPACK)
